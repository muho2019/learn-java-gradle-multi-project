plugins {
	id 'java'
	// 루트에는 플러그인을 적용하지 않고, 하위 모듈에서만 적용하도록 설정
	id 'org.springframework.boot' version '3.5.4' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'multi.module'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

// 모든 하위 모듈에 적용될 공통 설정
subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = project.group
	version = project.version

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		// 모든 모듈이 공통으로 사용하는 의존성
		implementation 'org.springframework.boot:spring-boot-starter'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}